#!/usr/bin/env zsh
# 
#   Copyright © 2010 Luka Vandervelden
#   Some rights reserved.
# 
# Redistribution and use in source and binary forms, with or without 
# modification, are permitted provided that the following conditions 
# are met: 
#  1. Redistributions of source code must retain the above copyright 
#     notice, this list of conditions and the following disclaimer 
#     in this position and unchanged. 
#  2. Redistributions in binary form must reproduce the above copyright 
#     notice, this list of conditions and the following disclaimer in the 
#     documentation and/or other materials provided with the distribution.
# 
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR(S) ``AS IS'' AND ANY EXPRESS OR 
# IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES 
# OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
# IN NO EVENT SHALL THE AUTHOR(S) BE LIABLE FOR ANY DIRECT, INDIRECT, 
# INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
# NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, 
# DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY 
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT 
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF 
# THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
# 

# Getting colors management tools from zsh utils
autoload -U colors
colors

info() {
	echo -e "${fg_bold[green]}--${fg_bold[white]} $1${reset_color}"
}

warning() {
	echo -e "${fg_bold[yellow]}-- WARNING: $1${reset_color}" >&2
}

error() {
	echo -e "${fg_bold[red]}-- ERROR: $1${reset_color}" >&2
}

pkgfile_filter() {
	sed -e "s/#name#/${name}/" \
	    -e "s/#version#/${version}/" \
	    -e "s/#url#/${url}/" \
	    -e "s/#packager#/${packager//\//\\/}/" \
	    -e "s/#maintainer#/${maintainer//\//\\/}/"
}

print_pkgfile() {
	# First we export needed variables for pkgfile_filter().
	if [[ -z "${name}" ]]; then
		export name="$(basename "$PRTCREATE_ROOT")"
	fi
	
	if [[ -z "$maintainer" ]]; then
		export maintainer="$packager" # If empty, we don’t loose much.
	fi
	
	# And then we check in we can use a particular predefined recipe, and 
	#+ then we build.
	if [[ -n "$PRTCREATE_TYPE" ]]; then
		for DIR in "$PRTCREATE_HOMEDIR/pkgfiles" "$PRTCREATE_SYSCONFDIR/pkgfiles"; do
			if [[ -e "$DIR/$PRTCREATE_TYPE" ]]; then
				cat "$DIR/$PRTCREATE_TYPE" | pkgfile_filter
				return $?
			fi
		done
		# If we are here, we did not found the desired recipe.
		error "Pkgfile type '$PRTCREATE_TYPE' is not available."
		exit 1
	else
		for DIR in "$PRTCREATE_SYSCONFDIR" "$PRTCREATE_HOMEDIR"; do
			if [[ -r "$DIR/default_recipe" ]]; then
				cat "$DIR/default_recipe" | pkgfile_filter
				return $?
			fi
		done
		# If we are here, we did not found any default_recipe.
		error "No standard recipe has been found on your system."
		error "$(basename "$PRTCREATE_COMMAND") is probably not installed correctly."
		exit 1
	fi
}

get_package_data() {
	if [[ -e configure ]]; then
		name="$(grep PACKAGE_NAME= configure | sed -e "s/PACKAGE_NAME=//;s/'//g")"
		version="$(grep PACKAGE_VERSION= configure | sed -e "s/PACKAGE_VERSION=//;s/'//g")"
	fi
}

parse_opts() {
	while [[ -n "$1" ]]; do
		case "$1" in
			-v|--version)
				echo "$(basename "$PRTCREATE_COMMAND") $PRTCREATE_VERSION"
				exit 0 ;;
			-h|--help)
				print_help
				exit 0 ;;
			-*)
				print_help
				exit 1 ;;
			*)
				if [[ -z "$PRTCREATE_TYPE" ]]; then
					PRTCREATE_TYPE="$1"
				else
					error "You may specify only one Pkgfile type."
				fi ;;
		esac
		shift 1
	done
}

print_help() {
	echo "usage: $(basename "$PRTCREATE_COMMAND") [Pkgfile type] [options]"
	echo "options:"
	echo "  -h, --help          prints this help message"
	echo "  -v, --version       prints prtcreate's version"
}

main() {
	parse_opts "$@"
	
	get_package_data
	
	for DIR in "$PRTCREATE_SYSCONFDIR" "$PRTCREATE_HOMEDIR"; do
		if [[ -e "$DIR/$PRTCREATERC" && -r "$DIR/$PRTCREATERC" ]]; then
			. "$DIR/$PRTCREATERC"
		fi
	done
	
	if [[ -e "$PRTCREATE_ROOT/Pkgfile" ]]; then
		error "A Pkgfile is already present in this directory."
	else
		print_pkgfile > "$PRTCREATE_ROOT/Pkgfile"
	fi
}

readonly PRTCREATE_VERSION="#VERSION#"
readonly PRTCREATE_COMMAND="$0"
readonly PRTCREATE_ROOT="$PWD"

PRTCREATE_SYSCONFDIR="#SYSCONFDIR#/prtcreate"
PRTCREATE_HOMEDIR="$HOME/.config/prtcreate"
PRTCREATERC="prtcreaterc"
PRTCREATE_TYPE=""

main "$@"

