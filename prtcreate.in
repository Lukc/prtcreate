#!/usr/bin/env zsh

# Getting colors management tools from zsh utils
autoload -U colors
colors

info() {
	echo -e "${fg_bold[green]}--${fg_bold[white]} $1${reset_color}"
}

warning() {
	echo -e "${fg_bold[yellow]}-- WARNING: $1${reset_color}" >&2
}

error() {
	echo -e "${fg_bold[red]}-- ERROR: $1${reset_color}" >&2
}

pkgfile_filter() {
	sed -e "s/#name#/${name}/" \
	    -e "s/#packager#/${packager//\//\\/}/" \
	    -e "s/#maintainer#/${maintainer//\//\\/}/"
}

print_pkgfile() {
	# First we export needed variables for pkgfile_filter().
	export name="$(basename "$PRTCREATE_ROOT")"
	if [[ -z "$maintainer" ]]; then
		export maintainer="$packager" # If empty, we donâ€™t loose much.
	fi
	
	# And then we check in we can use a particular predefined recipe, and 
	#+ then we build.
	if [[ -n "$PRTCREATE_TYPE" ]]; then
		for DIR in "$PRTCREATE_HOMEDIR/pkgfiles" "$PRTCREATE_SYSCONFDIR/pkgfiles"; do
			if [[ -e "$DIR/$PRTCREATE_TYPE" ]]; then
				cat "$DIR/$PRTCREATE_TYPE" | pkgfile_filter
				return $?
			fi
		done
		# If we are here, we did not found the desired recipe.
		error "Pkgfile type '$PRTCREATE_TYPE' is not available."
		exit 1
	else
		for DIR in "$PRTCREATE_SYSCONFDIR" "$PRTCREATE_HOMEDIR"; do
			if [[ -r "$DIR/default_recipe" ]]; then
				cat "$DIR/default_recipe" | pkgfile_filter
				return $?
			fi
		done
		# If we are here, we did not found any default_recipe.
		error "No standard recipe has been found on your system."
		error "$(basename "$PRTCREATE_COMMAND") is probably not installed correctly."
		exit 1
	fi
}

parse_opts() {
	while [[ -n "$1" ]]; do
		case "$1" in
			-v|--version)
				echo "$(basename "$PRTCREATE_COMMAND") $PRTCREATE_VERSION"
				exit 0 ;;
			-h|--help)
				print_help
				exit 0 ;;
			-*)
				print_help
				exit 1 ;;
			*)
				if [[ -z "$PRTCREATE_TYPE" ]]; then
					PRTCREATE_TYPE="$1"
				else
					error "You may specify only one Pkgfile type."
				fi ;;
		esac
		shift 1
	done
}

print_help() {
	echo "usage: $(basename "$PRTCREATE_COMMAND") [Pkgfile type] [options]"
	echo "options:"
	echo "  -h, --help          prints this help message"
	echo "  -v, --version       prints prtcreate's version"
}

main() {
	parse_opts "$@"
	
	for DIR in "$PRTCREATE_SYSCONFDIR" "$PRTCREATE_HOMEDIR"; do
		if [[ -e "$DIR/$PRTCREATERC" && -r "$DIR/$PRTCREATERC" ]]; then
			. "$DIR/$PRTCREATERC"
		fi
	done
	
	if [[ -e "$PRTCREATE_ROOT/Pkgfile" ]]; then
		error "A Pkgfile is already present in this directory."
	else
		print_pkgfile > "$PRTCREATE_ROOT/Pkgfile"
	fi
}

readonly PRTCREATE_VERSION="#VERSION#"
readonly PRTCREATE_COMMAND="$0"
readonly PRTCREATE_ROOT="$PWD"

PRTCREATE_SYSCONFDIR="#SYSCONFDIR#/prtcreate"
PRTCREATE_HOMEDIR="$HOME/.config/prtcreate"
PRTCREATERC="prtcreaterc"
PRTCREATE_TYPE=""

main "$@"

